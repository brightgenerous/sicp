(print "----------------")

;; -----

(print "--")
(print "問題3.12")

(define (append! x y)
  (set-cdr! (last-pair x) y)
  x)

(define (last-pair x)
  (if (null? (cdr x))
    x
    (last-pair (cdr x))))

(define x (list 'a 'b))
(define y (list 'c 'd))
(define z (append x y))
(print (format "z => ~a" z))
(print (format "(cdr x) => ~a" (cdr x)))
;
;       +-------+
;  x -> | a | ○ |
;       +-----+-+
;             |     +-------+
;             +---> | b | / |
;                   +-------+
;
(define w (append! x y))
(print (format "w => ~a" w))
(print (format "(cdr x) => ~a" (cdr x)))
(print (format "y => ~a" y))
;
;       +-------+
;  x -> | a | ○ |
;       +-----+-+
;             |     +-------+
;             +---> | b | ○ +-------+
;                   +-------+       |
;                                   v
;                               +-------+
;                          y -> | c | ○ |
;                               +-----+-+
;                                     |     +-------+
;                                     +---> | d | / |
;                                           +-------+
;


;; -----

(print "--")
(print "問題3.13")

(define (make-cycle x)
  (set-cdr! (last-pair x) x)
  x)

(define z (make-cycle (list 'a 'b 'c)))
;
;       +-------+
;  z -> | a | ○ | <-------------------+
;       +-----+-+                     |
;             |     +-------+         |
;             +---> | b | ○ |         |
;                   +-----+-+         |
;                         |     +-----+-+
;                         +---> | c | ○ |
;                               +-------+
;
(print (format "(car z) => ~a" (car z)))
(print (format "(cadr z) => ~a" (cadr z)))
(print (format "(caddr z) => ~a" (caddr z)))
(print (format "(cadddr z) => ~a" (cadddr z)))


;; -----

(print "--")
(print "問題3.14")

(define (mystery x)
  (define (loop x y)
    (if (null? x)
      y
      (let ((temp (cdr x)))
        (set-cdr! x y)
        (loop temp x))))
  (loop x '()))

; -----
;
;      +---+---+     +---+---+     +---+---+     +---+---+
; v -> | a | ○ + --> | b | ○ + --> | c | ○ + --> | d | / |
;      +---+---+     +---+---+     +---+---+     +---+---+
;
; -----
; 1回目 before set-cdr!
;                          temp
;                           |
;                           v
;         +---+---+     +---+---+     +---+---+     +---+---+
; x(v) -> | a | ○ + --> | b | ○ + --> | c | ○ + --> | d | / |
;         +---+---+     +---+---+     +---+---+     +---+---+
;
;         +---+
;    y -> | / |
;         +---+
;
; --
; 1回目 after set-cdr!
;                          temp
;                           |
;                           v
;         +---+---+     +---+---+     +---+---+     +---+---+
; x(v) -> | a | / |     | b | ○ + --> | c | ○ + --> | d | / |
;         +---+---+     +---+---+     +---+---+     +---+---+
;               ^
;               |
;               y
;
; -----
; 2回目 before set-cdr!
;                          temp
;                           |
;                           v
;         +---+---+     +---+---+     +---+---+
;    x -> | b | ○ + --> | c | ○ + --> | d | / |
;         +---+---+     +---+---+     +---+---+
;
;         +---+---+
; y(v) -> | a | / |
;         +---+---+
;
; --
; 2回目 after set-cdr!
;                          temp
;                           |
;                           v
;         +---+---+     +---+---+     +---+---+
;    x -> | b | ○ |     | c | ○ + --> | d | / |
;         +---+-+-+     +---+---+     +---+---+
;               |
;               v
;           +---+---+
;   y(v) -> | a | / |
;           +---+---+
;
; --
; 3回目 before set-cdr!
;                          temp
;                           |
;                           v
;         +---+---+     +---+---+
;    x -> | c | ○ + --> | d | / |
;         +---+-+-+     +---+---+
;
;           +---+---+     +---+---+
;      y -> | b | ○ + --> | a | / |
;           +---+---+     +---+---+
;
; --
; 3回目 after set-cdr!
;                        temp
;                         |
;                         v
;         +---+---+     +---+---+
;    x -> | c | ○ |     | d | / |
;         +---+-+-+     +---+---+
;               |
;               v
;           +---+---+     +---+---+
;      y -> | b | ○ + --> | a | / |
;           +---+---+     +---+---+
;
; --
; 4回目 before set-cdr!
;              temp
;               |
;               v
;         +---+---+
;    x -> | d | / |
;         +---+---+
;
;           +---+---+     +---+---+     +---+---+
;      y -> | c | ○ + --> | b | ○ + --> | a | / |
;           +---+---+     +---+---+     +---+---+
;
; --
; 4回目 after set-cdr!
;                       temp
;                        |
;                        v
;         +---+---+    +---+
;    x -> | d | ○ |    | / |
;         +---+-+-+    +---+
;               |
;               v
;           +---+---+     +---+---+     +---+---+
;      y -> | c | ○ + --> | b | ○ + --> | a | / |
;           +---+---+     +---+---+     +---+---+
;
(define v (list 'a 'b 'c 'd))
(print (format "v => ~a" v))
(define w (mystery v))
(print (format "v => ~a" v))
(print (format "w => ~a" w))

